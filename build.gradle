apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


group = "com.allenru"
archivesBaseName = "jackson-morphed-result"
version = '1.0.0'


repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.4'
}


jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Add, remove, and otherwise manipulate the properties of an object serialized with Jackson, at runtime without modifying the target object.'
        url 'https://github.com/Russell-Allen/jackson-morphed-result'

        scm {
          connection 'scm:git:git://github.com/Russell-Allen/jackson-morphed-result.git'
          developerConnection 'scm:git:git://github.com/Russell-Allen/jackson-morphed-result.git'
          url 'https://github.com/Russell-Allen/jackson-morphed-result.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            name 'Russell Allen'
            email 'allenru@gmail.com'
          }
        }
      }
    }
  }
}
